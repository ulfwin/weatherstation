'//******************************************************************************
'// Laird Technologies (c) 2013
'//
'// +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'// ######                                                                #######
'// ######        Library File meant to be #included in files             #######
'// ######                                                                #######
'// +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'//
'// This library contains contains a custom wind service manager and should be
'// #included in your app
'//
'//******************************************************************************

'//******************************************************************************
'// Definitions
'//******************************************************************************

'//******************************************************************************
'// Library Import
'//******************************************************************************

'//******************************************************************************
'// Global Variable Declarations
'//******************************************************************************

dim shWind as integer
dim chWndSpd as integer
dim wndSpd$ as string

'//******************************************************************************
'// Initialise Global Variable
'//******************************************************************************

shWind = 0
chWndSpd = 0

'//******************************************************************************
'// Function and Subroutine definitions
'//******************************************************************************

'//==============================================================================
'//==============================================================================
function AddCharWindSpeed(byval wndSpd as integer) as integer
  dim rc

  //----------------------------------------------------------------------------
  // Create the True Wind Speed Characteristic which has a UUID of 0x2A70
  //----------------------------------------------------------------------------
  dim mdAttr
  dim mdCccd
  dim mdSccd
  dim chProp
  //dim attr$

  //++++
  //Create the metadata for the value attribute in the characteristic
  //++++
  mdAttr = BleAttrMetadata(BLE_ATTR_ACCESS_OPEN,BLE_ATTR_ACCESS_NONE,2,0,rc)
  AssertResCode(rc,9200)
  //There is no CCCD in this characteristic
  mdCccd = BLE_CHAR_METADATA_ATTR_NOT_PRESENT
  //There is no SCCD in this characteristic
  mdSccd = BLE_CHAR_METADATA_ATTR_NOT_PRESENT
  //Create the Characteristic object
  chProp = BLE_CHAR_PROPERTIES_READ
  rc = BleCharNew(chProp,BleHandleUuid16(0x2A70),mdAttr,mdCccd,mdSccd)
  AssertResCode(rc,9210)

  //++++
  //Add the PRESENTATION FORMAT Descriptor (uint16, 10^-2, m/s, 0, 0)
  //++++
  rc = BleCharDescPrstnFrmt(0x06,-2,0x2712,0,0)
  AssertResCode(rc,9240)

  //++++
  //Commit the characteristic
  //++++
  rc = BleEncode16(wndSpd$,wndSpd,0)
  rc = BleCharCommit(shWind,wndSpd$,chWndSpd)
  AssertResCode(rc,9300)

endfunc rc


'//==============================================================================
'//==============================================================================
function RegWindService(byval wndSpd as integer) as integer
  dim rc
  dim hUuid1

  //----------------------------------------------------------------------------
  //Create a wind PRIMARY service attribute which has a custom uuid of 0x0002
  //----------------------------------------------------------------------------
  hUuid1 = BleHandleUuidSibling(hUuidCustom,0x0002)
  rc = BleSvcCommit(BLE_SERVICE_PRIMARY,hUuid1,shWind)
  AssertResCode(rc,9100)

  //----------------------------------------------------------------------------
  // Create the Battery Level Characteristic which has a UUID of 0x2A70
  //----------------------------------------------------------------------------
  rc = AddCharWindSpeed(wndSpd)
  AssertResCode(rc,9110)

endfunc rc


'//==============================================================================
'//==============================================================================
sub InitWindService(byval wndSpd as integer)
  dim rc

  // Initializing wndSpd$  
  rc = StrFill(wndSpd$,0,2)
  AssertResCode(rc,9100) 

  rc = RegWindService(wndSpd)

 // Not adding service to AD since it's custom made
  //if rc==0 then
  //   AddUuid(UUID_BATTERY_SERVICE)
  //endif

endsub

'//==============================================================================
'//==============================================================================
function SetWindSpeed(wndSpd as integer) as integer
  dim rc
  rc = BleEncode16(wndSpd$,wndSpd,0)
  rc = BleCharValueWrite(chWndSpd,wndSpd$)
  AssertResCode(rc,9500)

endfunc rc



'//******************************************************************************
'// Handler definitions
'//******************************************************************************

'//******************************************************************************
'// Other initialisations
'//******************************************************************************



